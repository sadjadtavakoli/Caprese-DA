plugins {
    id 'maven-publish'
    id 'signing'
    id 'eclipse'
}

allprojects {
    group = 'com.github.aserg-ufmg'
    version = '2.0.0'
}

subprojects {
    repositories {
        mavenCentral()
    }
}

wrapper {
    gradleVersion = '6.4.1'
}


ext.pomConfig = { pomName, pomDescription ->
    return {
        name = pomName
        description = pomDescription
        url = 'https://github.com/aserg-ufmg/RefDiff'
        licenses {
            license {
                name = 'The MIT License (MIT)'
                url = 'http://opensource.org/licenses/MIT'
            }
        }
        developers {
            developer {
                id = 'danilofs'
                name = 'Danilo Ferreira e Silva'
                email = 'danilofs@dcc.ufmg.br'
            }
        }
        scm {
            connection = 'scm:git:https://github.com/aserg-ufmg/RefDiff.git'
            developerConnection = 'scm:git:git@github.com:aserg-ufmg/RefDiff.git'
            url = 'https://github.com/aserg-ufmg/RefDiff/tree/master'
        }
    }
}

publishing {
    repositories {
        maven {
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = findProperty('ossrhUser')
                password = findProperty('ossrhPassword')
            }
            //url = "file:build/repo"
        }
    }
}
